;; RFC 8941
; sf            = ( dict-member / list-member) *( OWS "," OWS ((dict-member / list-member)))
sf-list       = list-member *( OWS "," OWS list-member )
list-member   = sf-item / inner-list
inner-list    = "(" *SP [ sf-item *( 1*SP sf-item ) *SP ] ")"
                parameters
parameters    = *( ";" *SP parameter )
parameter     = param-key [ "=" param-value ]
param-key     = key
key           = ( lcalpha / "*" )
                *( lcalpha / DIGIT / "_" / "-" / "." / "*" )
lcalpha       = %x61-7A ; a-z
param-value   = bare-item
sf-dictionary  = dict-member *( OWS "," OWS dict-member )
                            ; parameters have to be given after member-value or it
                            ; does not work properly
dict-member    = member-key (( "=" member-value ) / parameters )
member-key     = key
member-value   = sf-item / inner-list
sf-item   = bare-item parameters
bare-item = sf-integer / sf-decimal / sf-string / sf-token
            / sf-binary / sf-boolean
sf-integer = ["-"] 1*15DIGIT
sf-decimal  = ["-"] 1*12DIGIT "." 1*3DIGIT
sf-string = DQUOTE *chr DQUOTE
chr       = unescaped / escaped
unescaped = %x20-21 / %x23-5B / %x5D-7E
escaped   = "\" ( DQUOTE / "\" )
sf-token = ( ALPHA / "*" ) *( tchar / ":" / "/" )
sf-binary = ":" *(base64) ":"
base64    = ALPHA / DIGIT / "+" / "/" / "="
sf-boolean = "?" boolean
boolean    = "0" / "1"

;; RFC 7230
OWS            = *( SP / HTAB )
tchar          = "!" / "#" / "$" / "%" / "&" / "'" / "*"
              / "+" / "-" / "." / "^" / "_" / "`" / "|" / "~"
              / DIGIT / ALPHA
              ; any VCHAR, except delimiters